@inline ../../../templates/sidebar-page.um

@versionedPage
  @version 2.0.0

@topSection
  @title API
  @source [https://github.com/ocadotechnology/quantumjs/tree/master/docs/{{filename}}]: Edit Page
  @description
    Transforms for writing API documentation and building changelogs.

@contentSection
  @tableOfContents Contents

  @topic About
    This module provides transforms for writing the API documentation that you can see in the @hyperlink(/modules/quantum-api/#api)[API] section of this and the other module pages.

    It also provides the ability to generate @hyperlink(./#changelogs)[changelogs] from the api as well as @hyperlink(./#versioning)[versioned pages] when combined with @hyperlink(/modules/quantum-version/)[quantum-version]

  @topic Entities
    @inline entities.um

  @topic API
    @inline api.um

  @topic Examples
    Below are several examples with the code required and their output.

    @section Standalone
      Using the module to provide the transforms provides basic api documentation capabilities:

      @example
        @file quantum.config.js
          @noOutput
          @js
            const html = require('quantum-html')
            const api = require('quantum-api')

            const apiOptions = { ... }

            const htmlTransforms = {
              html: html.transforms(),
              // Add the api section renderers to the page
              api: api.transforms(apiOptions)
            }

            const pipeline = [
              html({ transforms: htmlTransforms })
            ]

            module.exports = {
              pipeline: pipeline,
              htmlTransforms: htmlTransforms,
              pages: '*.um'
            }
        @file
          @@um
            @function exampleFunction
              @description: Function description
              @param param1 [String]: Parameter
              @param? param1 [String]: Optional parameter

            @object exampleObject
              @description: Object description

    @section Changelogs
      @example
        @file quantum.config.js
          @js
            const api = require('quantum-api')

            const apiOptions = { ... }

            const html = require('quantum-html')
            const api = require('quantum-api')

            const htmlTransforms = {
              html: html.transforms(),
              // Add the api section renderers to the page
              api: api.transforms(apiOptions)
            }

            const pipeline = [
              // Parse changelogList entities
              api.fileTransform(apiOptions),
              html({ transforms: htmlTransforms })
            ]

            module.exports = {
              pipeline: pipeline,
              htmlTransforms: htmlTransforms,
              pages: '*.um'
            }

        @file path/index.um
          @@um
            @api moduleName
              @function exampleFunction
                @added 1.0.0
                @description: Function description
                @param param1 [String]: Parameter
                @param? param2 [String]: Optional parameter

                @param? param3 [Boolean]: Additional Parameter
                  @added 2.0.0

              @object exampleObject
                @description: Object description

        @file path/changelog.um
          @@um
            @changelogList
              @groupByApi false
              @reverseVisibleList true
              @process
                @inline path/index.um
              @version 2.0.0
                @description: Version 2.0.0 changes
              @version 1.0.0
                @description: Version 1.0.0 changes

        @output
          @changelogList
            @groupByApi false
            @reverseVisibleList true
            @process
              @api moduleName
                @function exampleFunction
                  @added 1.0.0
                  @description: Function description
                  @param param1 [String]: Parameter
                  @param? param2 [String]: Optional parameter

                  @param? param3 [Boolean]: Additional Parameter
                    @added 2.0.0

                @object exampleObject
                  @description: Object description

            @version 2.0.0
              @description: Version 2.0.0 changes
            @version 1.0.0
              @description: Version 1.0.0 changes

    @section Versioning
      You can use the same versioned api to produce multiple versioned pages that show a single version.

      See @hyperlink(/modules/quantum-verision/)[quantum-version] for more information.

      @example
        @file quantum.config.js
          @js
            const version = require('quantum-version')
            const api = require('quantum-api')

            const apiOptions = { ... }

            const html = require('quantum-html')
            const api = require('quantum-api')

            const htmlTransforms = {
              html: html.transforms(),
              // Add the api section renderers to the page
              api: api.transforms(apiOptions)
            }

            const pipeline = [
              // Parse the pages to generate versions (e.g. page/1.0.0/index.html)
              version.fileTransform(versionOptions),
              html({ transforms: htmlTransforms })
            ]

            module.exports = {
              pipeline: pipeline,
              htmlTransforms: htmlTransforms,
              pages: '*.um'
            }

        @file path/index.um
          @@um
            @versionedPage
              @version 1.0.0
              @version 2.0.0

            @api moduleName
              @function exampleFunction
                @added 1.0.0
                @description: Function description
                @param param1 [String]: Parameter
                @param? param2 [String]: Optional parameter

                @param? param3 [Boolean]: Additional Parameter
                  @added 2.0.0

              @object exampleObject
                @description: Object description

        @output
          @div .docs-example-code-heading: target/1.0.0/index.html

          @function exampleFunction
            @added 2.0.0
            @description: Function description

            @param param1 [String]: Parameter
            @param? param2 [String]: Optional parameter


          @div .docs-example-code-heading: target/2.0.0/index.html

          @function exampleFunction
            @description: Function description

            @updated 2.0.0
              Added an additional parameter

            @param param1 [String]: Parameter
            @param? param2 [String]: Optional parameter

            @param? param3 [Boolean]: Additional Parameter
              @added 2.0.0

          @div .docs-example-code-heading: target/index.html

          @function exampleFunction
            @description: Function description

            @updated 2.0.0
              Added an additional parameter

            @param param1 [String]: Parameter
            @param? param2 [String]: Optional parameter

            @param? param3 [Boolean]: Additional Parameter
              @added 2.0.0

    @section Custom Language
      To define a language to use with quantum api, you can build a custom language

      @example
        @file custom-language.js
          @noOutput
          @js
            @inline ../../../transforms/custom-language.js

        @file quantum.config.js
          @noOutput
          @js
            // These are additions required to a working quantum.config.js

            const customLanguage = require('./custom-language.js')

            ...

            // Add the new language to the api options
            const apiOptions = {
              languages: [
                customLanguage()
              ]
            }

            ...

            // Add the api transforms to htmlTransforms if they weren't there already
            const htmlTransforms = {
              api: require('quantum-api').transforms(apiOptions)
            }

            ...

            // Add the fileTransform for rendering changelog content
            const pipeline = [
              api.fileTransform(apiOptions)
            ]


        @file custom-language.css
          @noOutput
          @css
            @inline ../../../transforms/custom-language.css

        @file index.um
          @@um
            @custom something
              @description
                Description of something

              @thing thing1: Thing 1
              @thing thing2: Thing 2




